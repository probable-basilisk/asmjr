WHITESPACE = _{ " " | "\t" }

quote = _{ "\"" }
pretty_much_anything = { !(WHITESPACE | NEWLINE | quote | "," | ":" | "[" | "]") ~ ANY }
escape = _{ "\\" ~ ANY }

strlit = { quote ~ ( escape | (!quote ~ ANY) )* ~ quote }

value = @{ strlit | pretty_much_anything+ }
name = @{ pretty_much_anything+ }

comment = _{ ("#" | "//" | ";") ~ (!NEWLINE ~ ANY)* }

alias = { "reg" ~ name ~ "=" ~ value }
constant = { "const" ~ name ~ "=" ~ value }
label = { name ~ ":" }
op = { 
    ( name ~ (value ~ ",")* ~ value ~ "[" ~ value ~ "]" )
  | ( name ~ (value ~ (","? ~ value)*)? )
}
empty = @{ WHITESPACE* }

line = _{ (comment | alias | constant | label | op | empty) ~ comment? ~ NEWLINE }

program = _{SOI ~ line* ~ EOI}
